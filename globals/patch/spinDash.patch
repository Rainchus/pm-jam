%dont slow down spins
@Function E25DD0 % Normal spin duration frames (25 is vanilla)
{
    ADDIU v0, r0, 25`
}

@Function E25E10 % Normal spin frame # to begin slowing down
{
    ADDIU v0, r0, 25`
}

@Function E25E7C % Speedy spin duration frames
{
    ADDIU v0, r0, 35`
}

@Function E25EA4 % Speedy spin frame # to begin slowing down
{
    ADDIU v0, r0, 35`
}

%/* E26184 802B6424 C6000010 */  lwc1      $f0, 0x10($s0) only ran when mario should fall

#new:Data $spinHoverBool { 1 }

#new:Data $rayCastX { 0 }
#new:Data $rayCastY { 0 }
#new:Data $rayCastZ { 0 }
#new:Data $rayCastLength { 15.0 }
#new:Data $hitRx { 0 }
#new:Data $hitRz { 0 }
#new:Data $hitDirX { 0 }
#new:Data $hitDirZ { 0 }

@Hook E26184 { %802B6424
    PUSH[40] ra
    LAW t0, $spinHoverBool
    BEQ t0, r0, .noHover
    NOP

    LI a0, 8010EFC8
    LI a1, $rayCastX
    LI a2, $rayCastY
    LI a3, $rayCastZ
    LI t0, $rayCastLength
    LIF f0, 15.0
    SWC1 f0, 0000 (t0)
    SW t0, 0010 (sp) %set out length of raycast

    LI t0, $hitRx
    SW t0, 0014 (sp)

    LI t0, $hitRz
    SW t0, 0018 (sp)

    LI t0, $hitDirX
    SW t0, 001C (sp)

    LI t0, $hitDirZ
    SW t0, 0020 (sp)

    LW t1, 0028 (a0) %mario X
    SW t1, 0000 (a1)
    LW t1, 002C (a0) %mario Y
    SW t1, 0000 (a2)
    LW t1, 0030 (a0) %mario Z
    SW t1, 0000 (a3)

    JAL ~Func:player_raycast_below_cam_relative
    NOP
    LI t0, $rayCastLength
    LWC1 f0, 0000 (t0) %load distance to collider below mario
    LIF f2, 0.0
    C.EQ.S f0, f2
    BC1T .noHover
    NOP

    .fallState
    LI t0, 8010F250
    SB r0, 0001 (t0) %clear buffered spin
    JAL ~Func:set_action_state
    ORI a0, r0, 8 %falling state
    .noHover
    LWC1 f0, 0010 (s0) %restore from hook
    LUI at, 3F80 %restore from hook
    POP[40] ra
    J 802B642C
    NOP
}

%ACTION_STATE_FALLING %00000008

@Hook:Global 800E4A9C { %remove gravity
    LAW t0, $spinHoverBool
    BEQ t0, r0, .noHover
    NOP
    LAH t0, 80074026
    ANDI t1, t0, 2000
    BEQ t1, r0, .noHover
    NOP
    LIO t0, 8010EFC8
    LBU t1, 00B4 (t0) %load action state
    ORI t2, r0, 001A %spinning
    BEQ t1, t2, .isSpinning
    NOP
    %do gravity
    .noHover
    JAL 800E5DA4
    NOP
    .isSpinning
    J 800E4AA4
    NOP
}