#export .scrApi_Done2 2
#export .scrApi_Done1 1
#export .scrApi_Block 0

#export:Function $isBadgeEquipped {
    .Init
    LIO t2, 8010F498
    ADDIU t3, t2, 78 %end of list
    .loop
    LH t1, 0 (t2)
    BEQL a0, t1, .end
    ADDIU v0, r0, 1 %return true
    BNE t2, t3, .loop
    ADDIU t2, t2, 2
    ADDIU v0, r0, 0 %return false
    .end
    JR RA
    NOP
}

#export:Function:Battle $SetNewCursorPositionForProtoBatter {
    PUSH s0, s1, ra

    ADDU s0, a0, r0
    LW s1, 000C (a0) %load pointer to script args

    %assuming this will never be a Var[X]
    LW a0, 0000 (s1) %load arg0
    ADDIU a1, r0, FF81 %actor self ID
    BEQ a0, a1, .actorSelf
    NOP
    
    .actorSelf

    POP s0, s1, ra
    JR RA
    ORI v0, r0, 2
}

#export:Function $DrawMenuBox {
    ADDIU        SP, SP, -58
    SW        RA, 50 (SP)
    % setup call to DrawBox
    SW        R0, 10 (SP)    % posZ
    SW        A2, 14 (SP)    % width
    SW        A3, 18 (SP) % height
    SW        T1, 1C (SP) % opacity
    COPY      A2, A0 % posX
    COPY      A3, A1 % posY
    COPY      A1, T0 % flags
    CLEAR     A0 % flags
    SW        R0, 20 (SP) % fogRGB
    SW        R0, 24 (SP) % scaleX (unused)
    SW        R0, 28 (SP) % scaleY (unused)
    SW        R0, 2C (SP) % rotX (unused)
    SW        R0, 30 (SP) % rotY (unused)
    SW        R0, 34 (SP) % rotZ (unused)
    SW        R0, 38 (SP) % unk func
    SW        R0, 3C (SP) % unk func arg0
    SW        R0, 40 (SP) % txMtx (unused)
    SW        R0, 44 (SP) % txShiftY (unused)
    SW        R0, 48 (SP) % txShiftX (unused)
    JAL       800F52E0 %draw_box
    SW        R0, 4C (SP) % unk
    LW        RA, 50 (SP)
    JR        RA
    ADDIU        SP, SP, 58
}


#export:Function $ScrApi_XORVar { %arg0 = Var to XOR, arg1 = value to XOR with
    PUSH s0, s1, s2, ra
    ADDU s0, a0, r0 %move script pointer to s0
    LW s1, 000C (s0) %load ptr to script args

    JAL ~Func:get_variable
    LW a1, 0000 (s1) %load script args

    ADDU s2, v0, r0 %returned value of Var

    ADDU a0, s0, r0 %copy script ptr back to a0
    JAL ~Func:get_variable
    LW a1, 0004 (s1) %load script arg1

    XOR a2, s2, v0 %arg0 ^ arg1
    
    ADDU a0, s0, r0 %copy script ptr back to a0
    JAL ~Func:set_variable
    LW a1, 0000 (s1) %load variable constant

    POP s0, s1, s2, ra
    JR RA
    ORI v0, r0, .scrApi_Done2
}

#export:Function $ScrApi_Set2DBool {
    PUSH ra

    LW a1, 000C (a0) %load script ptr to args
    JAL ~Func:get_variable
    LW a1, 0000 (a1) %get variable
    SAW v0, $YAxisBool

    POP ra
    JR RA
    ORI v0, r0, .scrApi_Done2
}