#import SpriteAnimations.mpat

#define .oobYPositionRespawn -300

#new:Header $Header
{
	[MainScript] $Script_Main
	[Background] 80200000
	[EntryList] $EntryList
	[EntryCount] 2
}

#new:EntryList $EntryList
{
	~Vec4f:MainEntry
    ~Vec4f:Entry1
}

#new:Script $TexturePanningLava {
	Thread
        Set   *Var0  00000000 	%tex pan ID
        Set   *Var1  0 			%main U
        Set   *Var2  400`		%main V
        Set   *Var3  0			%Aux U
        Set   *Var4  0			%Aux V
        Set   *Var5  0
        Set   *Var6  1
        Set   *Var7  1
        Set   *Var8  1
        Set   *Var9  0
        Set   *VarA  0
        Set   *VarB  0
        Set   *VarC  0
        Exec  $Script_Update_Texture_Panner

		Set   *Var0  00000001 %tex pan ID
        Set   *Var1  0
        Set   *Var2  -400`
        Set   *Var3  0
        Set   *Var4  0
        Set   *Var5  0
        Set   *Var6  1
        Set   *Var7  1
        Set   *Var8  1
        Set   *Var9  0
        Set   *VarA  0
        Set   *VarB  0
        Set   *VarC  0
        Exec  $Script_Update_Texture_Panner
    EndThread
	Call  SetTexPanner  ( ~Model:lavaTopModel 00000000 )
	Call  SetTexPanner  ( ~Model:lavaBottomModel 00000001 )
	
	Return
	End
}

#new:Script $FlagController
{
    Call EnableModel (~Model:BowserFlag1 0)
    Call EnableModel (~Model:BowserFlag2 0)
    Call EnableModel (~Model:MarioFlag1 0)
    Call EnableModel (~Model:MarioFlag2 0)

    Thread
        Loop
            If *Flag_bis_05_checkpoint == 1
                Call EnableModel (~Model:BowserFlag1 0)
                Call EnableModel (~Model:BowserFlag2 0)
                Call EnableModel (~Model:MarioFlag1 1)
                Call EnableModel (~Model:MarioFlag2 0)
                Wait 18`
                Call EnableModel (~Model:MarioFlag1 0)
                Call EnableModel (~Model:MarioFlag2 1)
                Wait 18`
            Else
                Call EnableModel (~Model:MarioFlag1 0)
                Call EnableModel (~Model:MarioFlag2 0)

                Call EnableModel (~Model:BowserFlag1 1)
                Call EnableModel (~Model:BowserFlag2 0)
                Wait 18`
                Call EnableModel (~Model:BowserFlag1 0)
                Call EnableModel (~Model:BowserFlag2 1)
                Wait 18`
            EndIf
            Wait 1`
        EndLoop
    EndThread
    Return
    End
}

#new:Script $CheckIfFallenOOB {
	Thread
		Loop
			Call GetPlayerPos ( *Var0 *Var1 *Var2 )
			If *Var1 < .oobYPositionRespawn
				%Call SetPlayerPos ( ~Vec3d:RespawnMarker )
			EndIf
			Wait 1`
		EndLoop
	EndThread
	Return
	End
}


#new:Function_Init $Function_Init
{
	PUSH    RA
	LIA     A0, 800B0CF0
	LIA     A1, "bon_tex"
	JAL     800654F0 % sprintf
	RESERVED

	LI a0, 800D9668
	LI a1, "yki_bg"
	JAL     800654F0 % sprintf
	RESERVED
	
	CLEAR  V0
	JPOP   RA
}

#new:Script $Script_SetupMusic
{
	Call FadeOutMusic  ( 00000000 500` ) % usually between 500-1000
	Call ClearAmbientSounds ( 250` )
	Call SetMusicTrack ( 0 .Song:ShiverMountain 0 8 )
	Call FadeInMusic ( 0 .Song:ShiverMountain 0 5` 0 0 )
	Return
	End
}

#new:Script $Script_EnterMap
{
	Return
	End
}

#new:Script $Script_CreateExitTriggers
{
	Return
	End
}

#new:Script $SetCheckpointFlag {
    If *Flag_bis_05_checkpoint == 0
        Set *Flag_bis_05_checkpoint 1
    EndIf
    Return
    End
}

#new:Script $CheckPointRespawn {
    Bind  $SetCheckpointFlag .Trigger:FloorTouch ~Collider:CheckPointCollider 00000001 00000000
    Loop
        Call GetPlayerPos ( *Var0 *Var1 *Var2 )
        If *Var1 < -400` %fell oob
            If *Flag_bis_05_checkpoint == 0
                Call SetPlayerPos ( ~Vec3d:Check1 )
                Call PlaySoundAtModel (~Model:BoxPlat1 .Sound:Star 0 )
                Call PlayEffect (~FX:Sparkles:Star -20` 16` 0` 00000000 )
            Else
                Call SetPlayerPos ( ~Vec3d:Check2 )
                Call PlaySoundAtModel (~Model:BoxPlat2 .Sound:Star 0 )
                Call PlayEffect (~FX:Sparkles:Star 1120` 86` -149` 00000000 )
            EndIf
        EndIf
        Wait 1`
    EndLoop
    Return
    End
}

#new:Script $Script_802549D0 {
	Call  GotoMap   ( "bis_04" 00000000 )
	Wait  100`
	Return
	End
}

#new:Script $Make_Entities {
    Call MakeEntity    ( .Entity:BlueWarpPipe ~Vec4d:PipeExit ~Entry:Entry1 $Script_802549D0 ~Index:*Flag_block_bis_05 80000000 )
	Call MakeEntity ( .Entity:RedBlock ~Vec4d:PowerBlock .Item:AttackPiece1 80000000 )
	Call AssignBlockFlag   ( *Flag_block_bis_05 )
    Return
    End
}

#new:Script $runScripts { %for some reason sticking this directly in script main breaks the game????
    Exec $Make_Entities
    Exec $Script_SetupMusic
	Exec $TexturePanningLava
    Exec $Script_EnterMap
    Exec $CheckPointRespawn
    Exec $FlagController
    Exec $CheckIfFallenOOB
    Exec $checkIfStarlowUpgradeString
    Call ModifyColliderFlags ( 00000003 ~Collider:Lava 00000002 )
    Thread
        Call  ResetFromLava ( $LavaResetList )
    EndThread
    Return
    End
}

#new:LavaResetList $LavaResetList {
	~Collider:Lava ~Vec3d:Check1 % -503.0 0.0 -63.0
	FFFFFFFF 00000000 00000000 00000000
}

#new:Script $checkIfStarlowUpgradeString {
	Loop
		If *Flag_block_bis_05 == 1
			%Call ShowMessageAtScreenPos    ( 001D00DA  160`  40` ) % This is a Super Block. With this, you can upgrade  ...
			Call $upgradeStarlow ()
			BreakLoop
		EndIf
		Wait 1
	EndLoop
	
	Return
	End
}

#new:Script $Script_Main {
    Set   *GB_WorldLocation  .Location:GoombaRoad
    Call  SetCamPerspective  ( .Cam:Default 00000003 00000019 00000010 00001000 )
    Call  SetCamBGColor      ( .Cam:Default 00000000 00000000 00000000 )
    Call  SetCamEnabled      ( .Cam:Default .True )
    Call  SetCamLeadPlayer   ( .Cam:Default .False )
    Set *Flag_bis_05_checkpoint 0

    % Set *Var0 1
    % Thread
    %     Loop
    %         Call  $ScrApi_Set2DBool ( *Var0 )
    %         Wait 60`
    %         Call $ScrApi_XORVar ( *Var0 1 ) %Var0 ^ 1
    %     EndLoop
    % EndThread
    Exec $runScripts
    Return
    End
}