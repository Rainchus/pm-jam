#import SpriteAnimations.mpat

#new:Header $Header
{
	[MainScript] $Script_Main
	[Background] 80200000
	[EntryList] $EntryList
	[EntryCount] 1
}

#new:EntryList $EntryList
{
	~Vec4f:MainEntry
}

#new:Script $TexturePanningLava {
	Thread
        Set   *Var0  00000000 	%tex pan ID
        Set   *Var1  0 			%main U
        Set   *Var2  400`		%main V
        Set   *Var3  0			%Aux U
        Set   *Var4  0			%Aux V
        Set   *Var5  0
        Set   *Var6  1
        Set   *Var7  1
        Set   *Var8  1
        Set   *Var9  0
        Set   *VarA  0
        Set   *VarB  0
        Set   *VarC  0
        Exec  $Script_Update_Texture_Panner

		Set   *Var0  00000001 %tex pan ID
        Set   *Var1  0
        Set   *Var2  -400`
        Set   *Var3  0
        Set   *Var4  0
        Set   *Var5  0
        Set   *Var6  1
        Set   *Var7  1
        Set   *Var8  1
        Set   *Var9  0
        Set   *VarA  0
        Set   *VarB  0
        Set   *VarC  0
        Exec  $Script_Update_Texture_Panner
    EndThread
	Call  SetTexPanner  ( ~Model:lavaTopModel 00000000 )
	Call  SetTexPanner  ( ~Model:lavaBottomModel 00000001 )
	
	Return
	End
}

#new:Script $FlagController
{
    Set *Var0 0
    Call EnableModel ( ~Model:MarioFlag1 0 )
    Call EnableModel ( ~Model:MarioFlag2 0 )
    Loop
        Call EnableModel (~Model:BowserFlag1 1)
        Call EnableModel (~Model:BowserFlag2 0)
        Wait 18`
        Call EnableModel (~Model:BowserFlag1 0)
        Call EnableModel (~Model:BowserFlag2 1)
        Wait 18`
    EndLoop

    Return
    End
}


#new:Function_Init $Function_Init
{
	PUSH    RA
	LIA     A0, 800B0CF0
	LIA     A1, "bon_tex"
	JAL     800654F0 % sprintf
	RESERVED

	LI a0, 800D9668
	LI a1, "yki_bg"
	JAL     800654F0 % sprintf
	RESERVED
	
	CLEAR  V0
	JPOP   RA
}

#new:Script $Script_SetupMusic
{
	Call FadeOutMusic  ( 00000000 500` ) % usually between 500-1000
	Call ClearAmbientSounds ( 250` )
	Call SetMusicTrack ( 0 .Song:ShiverMountain 0 8 )
	Call FadeInMusic ( 0 .Song:ShiverMountain 0 5` 0 0 )
	Return
	End
}

#new:Script $Script_EnterMap
{
	Return
	End
}

#new:Script $Script_CreateExitTriggers
{
	Return
	End
}

#new:Script $SetCheckpointFlag {
    If *Flag_bis_05_checkpoint == 0
        Set *Flag_bis_05_checkpoint 1
    EndIf
    Return
    End
}

#new:Script $CheckPointRespawn {
    Bind  $SetCheckpointFlag .Trigger:FloorTouch ~Collider:CheckPointCollider 00000001 00000000
    Loop
        Call GetPlayerPos ( *Var0 *Var1 *Var2 )
        If *Var1 < -400` %fell oob
            If *Flag_bis_05_checkpoint == 0
                Call SetPlayerPos ( ~Vec3d:Check1 )
            Else
                Call SetPlayerPos ( ~Vec3d:Check2 )
            EndIf
        EndIf
        Wait 1`
    EndLoop
    Return
    End
}


#new:Script $Script_Main {
    Set   *GB_WorldLocation  .Location:GoombaRoad
    Call  SetCamPerspective  ( .Cam:Default 00000003 00000019 00000010 00001000 )
    Call  SetCamBGColor      ( .Cam:Default 00000000 00000000 00000000 )
    Call  SetCamEnabled      ( .Cam:Default .True )
    Call  SetCamLeadPlayer   ( .Cam:Default .False )
    Set *Flag_bis_05_checkpoint 0
    % Set *Var0 1
    % Thread
    %     Loop
    %         Call  $ScrApi_Set2DBool ( *Var0 )
    %         Wait 60`
    %         Call $ScrApi_XORVar ( *Var0 1 ) %Var0 ^ 1
    %     EndLoop
    % EndThread
    Exec $Script_SetupMusic
	Exec $TexturePanningLava
    Exec $Script_EnterMap
    Exec $CheckPointRespawn
    Exec $FlagController
    End
}